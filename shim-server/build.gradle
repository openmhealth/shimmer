buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        springBootVersion = "1.2.3.RELEASE"
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.4.1.RELEASE'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'

version = shimServerVersion

jar {
    baseName = 'shim-server'
}

bootRepackage {
    mainClass = "org.openmhealth.shim.Application"
}

ext {
    healthVaultSdkVersion = '1.6'
    omhSchemaSdkVersion = '1.0.0'
    signpostVersion = '1.2.1.2'
    springSecurityOAuthVersion = '2.0.7.RELEASE'
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    compile project(':java-shim-sdk')
    compile "commons-io:commons-io:2.4"
    compile "org.apache.httpcomponents:httpclient"
    compile "org.apache.httpcomponents:httpcore:4.3.3"
    compile "com.microsoft.hsg:hv-jaxb:${healthVaultSdkVersion}"
    compile "com.microsoft.hsg:hv-sdk:${healthVaultSdkVersion}"
    compile "com.fasterxml.jackson.core:jackson-annotations"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml"
    compile "joda-time:joda-time"
    compile "com.jayway.jsonpath:json-path"
    compile "com.github.fge:json-schema-validator:2.2.6"
    compile "org.openmhealth.schema:omh-schema-sdk:${omhSchemaSdkVersion}"
    compile "oauth.signpost:signpost-commonshttp4:${signpostVersion}"
    compile "oauth.signpost:signpost-core:${signpostVersion}"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "org.springframework.boot:spring-boot-starter-jetty"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.security.oauth:spring-security-oauth:${springSecurityOAuthVersion}"
    compile "org.springframework.security.oauth:spring-security-oauth2:${springSecurityOAuthVersion}"
    compile "org.codehaus.woodstox:woodstox-core-asl:4.2.1"

    testCompile "junit:junit"
    testCompile "org.mockito:mockito-core"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

task copyArchiveJarToDockerContext(dependsOn: build, type: Copy) {
    from 'build/libs'
    into '../docker/binary'
    include "${jar.archiveName}"
    rename { String fileName ->
        fileName.replace("${jar.archiveName}", "${jar.baseName}.jar")
    }
}